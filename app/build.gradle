plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk 30

    defaultConfig {
        applicationId "com.robby.pawoontest"
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        flavorDimensions "release"
    }

    productFlavors {
        mainapp {
            buildConfigField "String", "BASE_URL", "\"https://jsonplaceholder.typicode.com\""
        }
        devapp {
            applicationIdSuffix ".dev"
            buildConfigField "String", "BASE_URL", "\"https://jsonplaceholder.typicode.com\""
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def flavorName = variant.name
            def versionCode = variant.versionCode
            def versionName = variant.versionName
            if (flavorName.contains("main")) {
                if (variant.buildType.name == "release") {
                    outputFileName = "MainApp-Release-"+versionName+"-"+versionCode+"-"+getTimeStamp()+".apk"
                } else {
                    outputFileName = "MainApp-Debug-"+versionName+"-"+versionCode+"-"+getTimeStamp()+".apk"
                }
            } else if (flavorName.contains("dev")) {
                if (variant.buildType.name == "release") {
                    outputFileName = "DevApp-Release-"+versionName+"-"+versionCode+"-"+getTimeStamp()+".apk"
                } else {
                    outputFileName = "DevApp-Debug-"+versionName+"-"+versionCode+"-"+getTimeStamp()+".apk"
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

static def getTimeStamp() {
    def date = new Date()
    return date.format('yyyyMMdd.HHmm')
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'

    implementation 'androidx.room:room-runtime:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'

    debugImplementation 'com.readystatesoftware.chuck:library:1.1.0'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}